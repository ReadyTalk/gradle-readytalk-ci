buildscript {
  apply from: file('gradle/repositories.gradle')
}

plugins {
  id 'groovy'
  id 'jacoco'
  id 'idea'
  id 'maven-publish'
  id 'nu.studer.plugindev' version '1.0.3'
  id 'com.jfrog.artifactory' version '3.0.1'
  id 'nebula.info' version '2.2.0'
  id 'com.github.kt3k.coveralls' version '2.0.1'
}

apply plugin: 'com.readytalk.ci'

ext.isRelease = buildEnv.travisTag ==~ /v\d+\.\d+\.\d+/
bintrayUpload.onlyIf { isRelease }
artifactoryPublish.onlyIf { !isRelease }
if(isRelease) {
  ci.dependsOn publish
} else {
  version += '-SNAPSHOT'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
  compile (
    gradleApi(),
    localGroovy(),
    'com.netflix.nebula:gradle-info-plugin:1.9.5',
    'org.eclipse.jgit:org.eclipse.jgit:3.6.2.201501210735-r',
  )

  //Gradle doesn't yet handle dependency resolution conflicts for localGroovy()
  //See: http://forums.gradle.org/gradle/topics/conflicting_module_versions_with_groovy_gradle_2_0_rc_1
  testCompile('com.netflix.nebula:nebula-test:2.2.0') {
    exclude group:'org.codehaus.groovy'
  }

  //Allow testing of optional plugin configuration
  testCompile (
    'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1',
    'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0',
  )
}

test {
  maxParallelForks = 2
}

plugindev {
  authorEmail = 'stormbeta@gmail.com'
  authorId = 'stormbeta'
  authorName = 'Jason Miller'
  pluginDescription "CI Lifecycle and Conventions"
  pluginId 'com.readytalk.ci'
  pluginImplementationClass 'com.readytalk.gradle.plugins.CiLifecyclePlugin'
  pluginLicenses 'MIT'
  pluginTags 'gradle', 'conventions', 'publishing', 'ci'
  projectInceptionYear '2014'
  projectIssuesUrl = 'https://github.com/ReadyTalk/gradle-readytalk-ci/issues'
  projectUrl = 'http://oss.readytalk.com/gradle-readytalk-ci'
  projectVcsUrl = 'https://github.com/ReadyTalk/gradle-readytalk-ci.git'
  done()
}

artifactory {
  contextUrl = 'http://oss.jfrog.org/artifactory'
  publish {
    repository {
      repoKey = 'oss-snapshot-local'
      username = bintrayUser
      password = bintrayKey
      maven = true
    }
  }
}

bintray {
  user = bintrayUser
  key = bintrayKey
  pkg {
    userOrg = 'readytalk'
    repo = 'plugins'
    version.vcsTag = buildEnv.travisTag
  }
}

tasks.coveralls {
  dependsOn tasks.jacocoTestReport
  onlyIf { buildEnv.isCI }
}

tasks.ci.dependsOn tasks.coveralls

wrapper.gradleVersion = '2.3'
