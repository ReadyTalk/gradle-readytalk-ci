buildscript {
  apply from: file('gradle/repositories.gradle')
  apply from: file('gradle/credentials.gradle')
}

plugins {
  id 'groovy'
  id 'jacoco'
  id 'idea'
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version '0.9.1'
  id 'com.jfrog.artifactory' version '3.1.1'
  id 'com.github.kt3k.coveralls' version '2.4.0'
}

apply plugin: 'com.readytalk.ci'
apply plugin: 'com.readytalk.ci.version.snapshot'

sourceCompatibility = 1.7
targetCompatibility = 1.7

apply from: file('gradle/dependencies.gradle')

dependencies {
  configurations {
    //conflicts with localGroovy()
    testCompile.exclude module: 'groovy-all'
  }

  //Allow testing of optional plugin configuration
  testCompile (
    'com.gradle.publish:plugin-publish-plugin:0.9.1',
    'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1',
    'com.netflix.nebula:nebula-test:3.0.0',
    'org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1',
  )
}

pluginBundle {
  website = 'http://oss.readytalk.com/gradle-readytalk-ci'
  vcsUrl = 'https://github.com/ReadyTalk/gradle-readytalk-ci.git'
  description = "CI Lifecycle and Conventions"
  tags = [ 'gradle', 'conventions', 'publishing', 'ci' ]

  plugins {
    ciPlugin {
      id = 'com.readytalk.ci'
      displayName = 'ReadyTalk CI Plugin'
    }
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact publishPluginJar {
        classifier 'sources'
      }
      artifact publishPluginGroovyDocsJar {
        classifier 'groovydoc'
      }
      artifact publishPluginJavaDocsJar {
        classifier 'javadoc'
      }
    }
  }
}

artifactory {
  contextUrl = 'http://oss.jfrog.org/artifactory'
  publish {
    repository {
      repoKey = 'oss-snapshot-local'
      username = bintrayUser
      password = bintrayKey
      maven = true
    }
  }
}

tasks.coveralls {
  dependsOn 'jacocoTestReport'
  onlyIf { buildEnv.ci }
}

jacocoTestReport {
  dependsOn 'integTest'
  reports {
    xml.enabled true
    html.destination "${buildDir}/jacocoHtml"
  }
}

tasks.withType(Test) { testTask ->
  // Run tests in parallel
  testTask.maxParallelForks = Runtime.runtime.availableProcessors()
}

tasks.ci.dependsOn tasks.coveralls
artifactoryPublish.onlyIf { ! buildEnv.release }

wrapper.gradleVersion = '2.6'
